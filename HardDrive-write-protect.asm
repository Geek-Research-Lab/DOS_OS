CSEG					SEGMENT
						ASSUME	CS:CSEG
						ORG		100H

START:					JMP		INITIALIZE

SIG:					DB		"WRITE_DRIVE"

OLDINT13				DD		?							; ORIGINAL INTERRUPT 13 VECTOR
SWITCH					DB		0FH							; ON/OFF SWITCH FOR PROTECTION

NEWINT13				PROC	FAR
															; CHECK FOR
						CMP		AH, 03H						; DISK WRITE
						JZ		CHECKSTAT

						CMP		AH, 05H						; DISK FORMAT
						JZ		CHECKSTAT

						CMP		AH, 06H						; FORMAT TRACK AND SET BAD SECTOR
						JZ		CHECKSTAT

						CMP		AH, 07H						; FORMAT DRIVE
						JZ		CHECKSTAT

						CMP		AH, 0BH						; WRITE LONG
						JZ		CHECKSTAT

						CMP		AH, 0FH						; WRITE SECTOR BUFFER
						JZ		CHECKSTAT

						CMP		AH, 13H						; DRIVE DIAGNOSTIC
						JZ		CHECKSTAT

						CMP		AH, 19H						; PARK HEADS ON A PS/2
						JZ		CHECKSTAT

						CMP		AH, 1AH						; FORMAT ESDI DISK ON A PS/2
						JZ		CHECKSTAT

CONTINUE:				JMP		CS:[OLDINT13]				; CALL THE REAL INT 13H HANDLER

CHECKSTAT:				CMP		SWITCH, 00H					; IS THE WRITE PROTECT ON?
						JNZ		CONTINUE
						CMP		DL, 00H						; IS THE A-DRIVE SELECTED?
						JZ		CONTINUE
						CMP		DL, 01H						; IS THE B-DRIVE SELECTED?
						JZ		CONTINUE

						MOV		AH, 03H						; MAKE REQUEST ERROR.. OH! OH! WP ERROR
						STC									; SET CARRY FOR FAILURE
						RET		2

NEWINT13				ENDP

INITIALIZE:				MOV		DX, OFFSET SIG				; OFFSET TO BEGIN CODE SEARCH
						MOV		AX, CS						; LOOK THROUGH FOR PSPs...
						MOV		ES, AX						; TO FIND A MATCH FOR SIGNATURE STRING

NEXTSEG:				DEC		AX
						MOV		DS, AX
						MOV		SI, DX
						MOV		DI, DX

						MOV		CX, 0004H
						CLD
						REPE	CMPSW
						JNZ		NOTFOUND

						CMP		DS:SWITCH, 0FH
						JNZ		TOGGLESW

NOTFOUND:				CMP		AX, 0001H
						JNZ		NEXTSEG

						MOV		SWITCH, 00H

						MOV		AX, 3513H					; GET THE OLD INT 13H VECTOR
						INT		21H							; SAVE IT

						MOV		WORD PTR CS:[OLDINT13], BX
						MOV		WORD PTR CS:[OLDINT13+2], ES

						PUSH	CS
						POP		DS

						MOV		DX, OFFSET PROTECT_ON
						MOV		AH, 09H
						INT		21H							; WRITE PROTECTION IS ON

						MOV		DX, OFFSET NEWINT13
						MOV		AX, 2513H
						INT		21H							; POINT TO OUR NEW INTERRUPT

						LEA		DX, INITIALIZE
						ADD		DX, 15
						MOV		CL, 4
						SHR		DX, CL

						MOV		AX, 3100H
						INT		21H

; TOGGLE THE SWITCH IF THE PROGRAM HAS BEEN LOADED EARLIER
TOGGLESW:				NOT		DS:SWITCH
						CMP		DS:SWITCH,00H				; IS SWITCH ON?
						JZ		ON
						MOV		DX, OFFSET PROTECT_OFF
						JMP		EXIT
ON:						MOV		DX, OFFSET PROTECT_ON

EXIT:					MOV		AH, 09H
						PUSH	CS
						POP		DS
						INT		21H							; PRINT THE STATUS
						INT		20H

PROTECT_ON				DB		"YOUR HARD DRIVE IS WRITE PROTECTED"
PROTECT_OFF				DB		"YOUR HARD DRIVE IS NOT WRITE PROTECTED"

CSEG					ENDS

						END		START



